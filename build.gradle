plugins {
    id "java"
    id "maven" // install jar files to the local repo: $ gradle install
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.3.0"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk"
version = "0.3.8"
description = "Import records from Zendesk."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-test:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
    testCompile "org.embulk:embulk-deps-guess:0.9.23"
    testCompile "org.mockito:mockito-core:2.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.zendesk.ZendeskInputPlugin"
    category = "input"
    type = "zendesk"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    authors = [ "Hieu Duong" ]
    email = [ "duongminhhieu89@gmail.com" ]
    summary = "Embulk Input Zendesk input plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-input-zendesk"
    licenses = []

    generateRubyCode = false
    into("lib/embulk/input/") {
        from "lib/embulk/input/zendesk.rb"
    }
    into("lib/embulk/guess/") {
        from "lib/embulk/guess/zendesk.rb"
    }
}

gemPush {
    host = "https://rubygems.org"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}