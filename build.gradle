plugins {
    id "com.jfrog.bintray" version "1.1"
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "com.treasuredata.embulk.plugins"
version = "0.3.10"
description = "Loads records From Zendesk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly  "org.embulk:embulk-core:0.10.18"
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.10.18:tests"
    testCompile 'org.embulk:embulk-junit4:0.10.18'
    testCompile "org.embulk:embulk-deps:0.10.18"
    testCompile "org.mockito:mockito-core:2.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.zendesk.ZendeskInputPlugin"
    category = "input"
    type = "zendesk"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = uri("https://treasuredata.jfrog.io/treasuredata/libs-release-local")
            credentials {
                username = System.getenv("TD_ARTIFACTORY_USERNAME")
                password = System.getenv("TD_ARTIFACTORY_PASSWORD")
            }
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}

checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}

checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}

task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gemPush {
    host = "https://treasuredata.jfrog.io/treasuredata/api/gems/rubygems-local"
}

gem {
    authors = [ "hieu.duong" ]
    email = [ "tuan@treasure-data.com" ]
    summary = "Facebook Leads input plugin for Embulk"
    homepage = "https://github.com/treasure-data/td-embulk-plugins/tree/embulk-input-facebook_leads"
    licenses = [ "MIT" ]

    generateRubyCode = false
    into("lib/embulk/input/") {
        from "lib/embulk/input/zendesk.rb"
    }
    into("lib/embulk/guess/") {
        from "lib/embulk/guess/zendesk.rb"
    }
}

clean { delete "${project.name}.gemspec" }

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}